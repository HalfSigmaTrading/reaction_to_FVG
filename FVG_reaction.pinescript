//@version=5
strategy("Reaction au FVG", overlay=true, initial_capital=20000, default_qty_type=strategy.fixed, default_qty_value=1, margin_long=0, margin_short=0)

// Inputs
heures_trading = input.string("0300-1130", "Heures de trading NY EST")
tp = input.float(10.25, "Take profit (handles)", step=0.25, group="Fixed profit taking") // 1 point = 4 ticks (adapté à NQ)
drawRWR = input.bool(true, "Show SL and TP on chart", group="Fixed profit taking")
slmax = input.float(37, "Maximum SL (handles)", step=0.25, group="ATR based Stop Loss settings")
atrPeriod = input.int(title='ATR Period', defval=5, minval=1, group="ATR based Stop Loss settings", tooltip="This setting is used in the raw ATR value calculation.  Lower values will be more reactive to recent price movement, while higher values will better indicate loger-term trend.\n\n" +
                 "Most often this is set at either 14 or 21.\nDefault: 3")
atrMultiplier = input.float(title='ATR Band Scale Factor', defval=3, step=0.1, minval=0.01, group="ATR based Stop Loss settings", tooltip="Scaling factor (aka multiplier) for the ATR to use for plotting the ATR bands.  " +
                     "This will usually be between 1 and 3.\n\nDefault: 2.5")
drawATRbands = input.bool(false, "Show ATR channel", group="ATR based Stop Loss settings")
timeoutPeriod = input.int(30, "Time based Exit (minutes)", minval=5, maxval=120)


// Restriction des heures de trading
inKillzone = not na(time(timeframe.period, heures_trading, "America/New_York"))

// Vérifier qu'aucun trade n'est en cours
noTrade = strategy.position_size == 0
ema50 = ta.ema(close,50)

// Conditions pour un scénario BULLISH avec BISI - 3 bougies drop
// B6-B5-B4 forment BISI
// B3-B2-B1 plonge dans le BISI
// B0 cloture au-dessus du BISI
// SL at B6 low
var float slFVG = 0
bisi_b6_b5_b4 = high[6] < low[4]
black_b3_b2_b1 = (close[3] < open[3]) and (close[2] < open[2]) and (close[1] < open[1])
drop_b3_b2_b1 = ((low[3] < high[6]) or (low[2] < high[6])  or (low[1] < high[6])) and ((low[3] > low[6]) and (low[2] > low[6])  and (low[1] > low[6])) 
strong_close_b0 = close > low[4]
longEntry_3candles = bisi_b6_b5_b4 and black_b3_b2_b1 and drop_b3_b2_b1 and strong_close_b0 and noTrade

// Conditions pour un scénario BULLISH avec BISI - 2 bougies drop
// B6-B5-B4 forment BISI
// B3-B2-B1 plonge dans le BISI
// B0 cloture au-dessus du BISI
// SL at B6 low
bisi_b5_b4_b3 = high[5] < low[3]
black_b2_b1 = (close[2] < open[2]) and (close[1] < open[1])
drop_b2_b1 = ((low[2] < high[5])  or (low[1] < high[5])) and ((low[2] > low[5])  and (low[1] > low[5])) 
strong_close_b0 := close > low[3]
longEntry = bisi_b5_b4_b3 and black_b2_b1 and drop_b2_b1 and strong_close_b0 and noTrade

// Conditions pour un scénario BEARISH avec SIBI
// B6-B5-B4 forment SIBI
// B3-B2-B1 plonge dans le SIBI
// B0 cloture sous le SIBI
// SL at B6 high
sibi_b6_b5_b4 = low[6] > high[4]
green_b3_b2_b1 = (close[3] > open[3]) and (close[2] > open[2]) and (close[1] > open[1])
spike_b3_b2_b1 = (low[6] < high[3]) or (low[6] < high[2])  or (low[6] < high[1]) and ((high[3] < high[6]) and (high[2] < high[6])  and (high[1] < high[6])) 
weak_close_b0 = (close < high[4])
shortEntry = sibi_b6_b5_b4 and green_b3_b2_b1 and spike_b3_b2_b1 and weak_close_b0  and noTrade

if longEntry
    slFVG := math.abs(close - low[6])
else if shortEntry
    slFVG := math.abs(close - high[6])

//  ATR based Stop Loss computation
scaledATR = ta.atr(atrPeriod) * atrMultiplier
sl = math.round(math.min(slmax,scaledATR, slFVG))  // Stop Loss defined by ATR
upperATRBand = close + scaledATR
lowerATRBand = close - scaledATR

// Définition des targets et SL
point = syminfo.mintick * 4  // Ajustement en fonction de l'échelle de l'actif
longTP = close + tp * point
longSL = close - sl * point
shortTP = close - tp * point
shortSL = close + sl * point

// TradingView alerts variables
//    - plot_0 is the Take Profit
//    - plot_1 is the Stop Loss
plot(shortEntry ? shortTP : longTP, title="Take Profit", color = color.new(color.white, 20), display = display.status_line)
plot(shortEntry ? shortSL : longSL, title="Stop Loss", color = color.new(color.white, 20), display = display.status_line)

// Placer les ordres
if shortEntry and inKillzone //and canTrade
    strategy.order("Short", strategy.short, qty=1, limit=close)
    strategy.exit("Short Exit", from_entry="Short", stop=shortSL, limit=shortTP)
    label.new(bar_index, high + 1, text="Short", color=color.red, textcolor=color.white, style=label.style_label_down)
    if drawRWR // Stop Zone (Red) and Target Zone (Green)
        box.new(left=bar_index, right=bar_index + 5, top=shortSL, bottom=close, border_color=color.red, bgcolor=color.new(color.red, 80), text="SL: " + str.tostring(shortSL-close), text_size = size.small, text_halign = text.align_right, text_valign = text.align_top)
        box.new(left=bar_index, right=bar_index + 5, top=close, bottom=shortTP, border_color=color.green, bgcolor=color.new(color.green, 80))

if longEntry and inKillzone  //and canTrade
    strategy.order("Long", strategy.long, qty=1, limit=close)
    strategy.exit("Long Exit", from_entry="Long", stop=longSL, limit=longTP)
    label.new(bar_index, low - 1, text="Long", color=color.green, textcolor=color.white, style=label.style_label_up)
    if drawRWR // Stop Zone (Red) and Target Zone (Green)
        box.new(left=bar_index, right=bar_index + 5, top=close, bottom=longSL, border_color=color.red, bgcolor=color.new(color.red, 80), text="SL: " + str.tostring(close-longSL), text_size = size.small, text_halign = text.align_right, text_valign = text.align_bottom)
        box.new(left=bar_index, right=bar_index + 5, top=longTP, bottom=close, border_color=color.green, bgcolor=color.new(color.green, 80))

var int entryTime = 0
if (strategy.position_size[1] == 0) and (strategy.position_size != 0)
    // trade just opened
    entryTime := time

// Time-based exit
if strategy.position_size != 0
    if (time - entryTime) >= timeoutPeriod * 60000  // Convert minutes to milliseconds
        strategy.close_all(comment="Time Exit")

// Plot current position
bgcolor(strategy.position_size > 0 ? color.new(color.blue, 90) : strategy.position_size < 0 ? color.new(color.blue, 90) : na)

//ATR Band Plotting
plot(drawATRbands ? upperATRBand : na, title="Upper ATR Band", color=color.rgb(0, 255, 0, 50), linewidth=2)
plot(drawATRbands ? lowerATRBand : na, title="Lower ATR Band", color=color.rgb(255, 0, 0, 50), linewidth=2)
